' {$STAMP BS2}
' {$PBASIC 2.5}

DEBUG "Program Running!"

i                 VAR     Byte    ' —≠ª∑±‰¡ø
leftStop          CON     729
rightStop         CON     750
moveDelta         CON     60

' main()

DO
  IF (IN5 = 0) AND (IN7 = 0) THEN
    GOSUB StepBack
    GOSUB TurnLeft
    GOSUB TurnLeft
  ELSEIF (IN5 = 0) THEN           ' ◊Û¥•–Î”ˆµΩ’œ∞≠
    GOSUB StepBack
    GOSUB TurnRight
  ELSEIF (IN7 = 0) THEN           ' ”“¥•–Î”ˆµΩ’œ∞≠
    GOSUB StepBack
    GOSUB TurnLeft
  ELSE
    GOSUB StepForward
  ENDIF
LOOP


StepForward:
  PULSOUT 13, leftStop + moveDelta
  PULSOUT 12, rightStop - moveDelta
  PAUSE 20
  RETURN

StepBack:
  FOR i = 0 TO 30
    PULSOUT 13, leftStop - moveDelta
    PULSOUT 12, rightStop + moveDelta
    PAUSE 20
  NEXT
  RETURN

TurnLeft:
  FOR i = 0 TO 14
    PULSOUT 13, leftStop - moveDelta
    PULSOUT 12, rightStop - moveDelta
    PAUSE 20
  NEXT
  RETURN

TurnRight:
  FOR i = 0 TO 14
    PULSOUT 13, leftStop + moveDelta
    PULSOUT 12, rightStop + moveDelta
    PAUSE 20
  NEXT
  RETURN
